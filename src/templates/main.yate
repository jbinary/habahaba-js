module "main"

external scalar maxPriority(nodeset)
external array splitlines(scalar)
external scalar formatDate(nodeset, scalar)
external scalar toJSON(scalar)

func maxPriorityPresence(nodeset contact) {
    prio = maxPriority(contact)
    if (prio || prio == 0) {
        contact.presences[.priority == prio]
    } else {
        contact.presences[0]
    }
}

match / __main__ {
    <div id="wrapper">
        <div id="header">
            <div id="logo">
            </div>
        </div>
        <div id="left-column">
            apply .my_presence render_my_presence
            apply .roster render_roster
        </div>
        apply / working_area
        apply /.jingle jingle
    </div>
}

func selected() {
    @selected = "selected"
}

func slideAnimation() {
    @onshow = "$(this).slideDown('slow');"
    @onhide = "$(this).slideUp('slow');"
}

func draggable() {
    "$(this).drags();"
}

match / render_my_presence {
    apply .my_presence render_my_presence
}

match .my_presence render_my_presence {
    <div class="border">
        <div id="user-block">
            <h4>{ ..nickname }</h4>
            /*<a href="#" id="personal-info-button"></a>
            <a href="#" id="blog-button"></a>*/
            <br class="clr"/>
            <img src="img/userpic.png" alt="ava"/>
            <p id="status">{ .status }</p>
            <select>
                <option>
                "Available"
                </option>
                <option>
                if .show == "away" { selected() }
                "Away"
                </option>
                <option>
                if .show == "xa" { selected() }
                "Extended Away"
                </option>
                <option>
                if .show == "dnd" { selected() }
                "Do Not Disturb"
                </option>
                <option>Offline</option>
            </select>
            <br class="clr"/>
       </div>
    </div>
}

include "roster.yate"
include "working_area.yate"
include "jingle.yate"
